buildscript {

    //androidx
    ext.androidx_crypto_version = "1.1.0-alpha03"
    ext.androidx_swiperefresh_version = "1.1.0"
    ext.androidx_constraintlayout_version = "2.1.2"
    ext.androidx_activity_version = "1.1.0" // TODO: update, but breaks animations
    ext.androidx_appcompat_version = "1.4.0"
    ext.androidx_core_version = "1.7.0"
    ext.androidx_espresso_version = "3.4.0"
    ext.androidx_fragment_version = "1.3.0-alpha07" // TODO: update but breaks animations
    ext.androidx_lifecycle_version = "2.4.0"
    ext.androidx_lifecycle_testing_version = "2.1.0"
    ext.androidx_nav_version = "2.3.5"
    ext.androidx_test_ext_junit_version = "1.1.3"

    //google
    ext.google_material_version = "1.4.0"
    ext.google_services_version = "4.3.4"
    ext.firebase_crashlytics_version = '2.5.2'
    ext.firebase_crashlytics_ktx_version = "18.2.6"

    //kotlin
    ext.kotlin_version = '1.6.10'
    ext.kotlinx_coroutines_android_version = '1.6.0-RC2'
    ext.kotlinx_serialization_json_version = "1.0.0"
    ext.kotlin_serialization_converter_version = "0.8.0"

    //test
    ext.junit_version = "4.13"
    ext.mockk_version = "1.11.0"
    ext.coroutines_test_version = "1.3.7"

    //3rd-party dependency versions
    ext.desugar_jdk_libs_version = "1.1.5"
    ext.libsodium_jni_version = "2.0.2"
    ext.rootbeer_version = "0.0.8"
    ext.joda_time_version = "2.10.6.2"
    ext.groupie_version = "2.10.0"
    ext.okhttp3_version = "4.8.1"
    ext.retrofit_version = "2.9.0"
    ext.timber_version = "5.0.1"
    ext.sticky_header_version = "1.0.1"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$androidx_nav_version"
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$firebase_crashlytics_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext.getVersionCode = { ->
    try {
        def runNumber = System.getenv('GITHUB_RUN_NUMBER')
        if (runNumber) {
            return Integer.parseInt(runNumber)
        }
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/main'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
                freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
                freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi"]
                freeCompilerArgs += ["-Xuse-experimental=kotlinx.serialization.ExperimentalSerializationApi"]
                freeCompilerArgs += ["-Xuse-experimental=kotlin.ExperimentalStdlibApi"]
                freeCompilerArgs += ["-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"]
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}