plugins { id "com.starter.easylauncher" version "4.0.0" }
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlinx-serialization'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "nl.rijksoverheid.dbco"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode project.getVersionCode()
        versionName '1.3.1.' + project.getVersionCode()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "FEATURE_LOGGING", "false"
        buildConfigField "boolean", "FEATURE_SSL_PINNING", "true"

        manifestPlaceholders = [appLabel: "@string/app_name"]
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GIT_VERSION", "\"${getGitHash()}\""
        }

        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "String", "GIT_VERSION", "\"dev\""
        }
    }

    signingConfigs {
        firebase {
            storeFile file('demo_keystore.jks')
            storePassword 'store_password'
            keyAlias = 'key_alias'
            keyPassword 'key_password'
        }
    }

    flavorDimensions "environment"

    productFlavors {

        dbcoTest {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "false"
            buildConfigField "boolean", "FEATURE_SECURE_WINDOW", "false"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "String", "GGD_PUBLIC_KEY", "\"09jfya2/x7yrn8Cp3KTH2sKj26+g26qup+DG26HNoq62rb7Lu82c18jEua4=\""
            buildConfigField "String", "BASE_API_URL", "\"https://api-test.bco-portaal.nl/\""
            buildConfigField "String", "OBFUSCATED_SSL_PIN", "\"07/OyrnZ5drNrMG4wJzf3MCeycja5bqbmaqquM/Xyabh3cyZosvBycSrv6Lh2tver6id\""
            manifestPlaceholders = [appLabel: "@string/app_name"]
            signingConfig signingConfigs.firebase
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            applicationIdSuffix ".test"
        }

        dbcoAcc {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "true"
            buildConfigField "boolean", "FEATURE_SECURE_WINDOW", "false"
            buildConfigField "boolean", "FEATURE_LOGGING", "false"
            buildConfigField "String", "GGD_PUBLIC_KEY", "\"06m+yJ7CosXgyMycy5mbmMimzKaeq72ooMi6uMu/x9vC2by2zMvav+Db2Lg=\""
            buildConfigField "String", "BASE_API_URL", "\"https://api-acc.bco-portaal.nl/\""
            buildConfigField "String", "OBFUSCATED_SSL_PIN", "\"0+C/qsfNyJbPyNu/mMO5vKXGnOCfxMW82cvAvsPNnaii27nDzJ+avqnZvqTh2tver6id\""
            manifestPlaceholders = [appLabel: "@string/app_name"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            signingConfig signingConfigs.firebase
            applicationIdSuffix ".acc"
        }

        dbcoStaging {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "true"
            buildConfigField "boolean", "FEATURE_SECURE_WINDOW", "false"
            buildConfigField "boolean", "FEATURE_LOGGING", "false"
            buildConfigField "String", "GGD_PUBLIC_KEY", "\"09ylocqm26msveC4yb3Yma6s3s+qqM+lmtykzsLDm5rCqqyouKqX3pbfr9o=\""
            buildConfigField "String", "BASE_API_URL", "\"https://api-staging.bco-portaal.nl/\""
            buildConfigField "String", "OBFUSCATED_SSL_PIN", "\"0+C/qsfNyJbPyNu/mMO5vKXGnOCfxMW82cvAvsPNnaii27nDzJ+avqnZvqTh2tver6id\""
            manifestPlaceholders = [appLabel: "@string/app_name"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            signingConfig signingConfigs.firebase
            applicationIdSuffix ".staging"
        }

        dbcoProd {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "true"
            buildConfigField "boolean", "FEATURE_SECURE_WINDOW", "true"
            buildConfigField "boolean", "FEATURE_LOGGING", "false"
            buildConfigField "String", "GGD_PUBLIC_KEY", "\"06mlvt7fvb23w8zdqJa7qJ26vd/JodektpbBzavbu663qqvLuNvbyd2Xy+U=\""
            buildConfigField "String", "BASE_API_URL", "\"https://api.bco-portaal.nl/\""
            buildConfigField "String", "OBFUSCATED_SSL_PIN", "\"0+C/qsfNyJbPyNu/mMO5vKXGnOCfxMW82cvAvsPNnaii27nDzJ+avqnZvqTh2tver6id\""
            manifestPlaceholders = [appLabel: "@string/app_name"]
            signingConfig signingConfigs.firebase
        }
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    lintOptions {
        disable "UnsafeExperimentalUsageError", "UnsafeExperimentalUsageWarning", "NullSafeMutableLiveData"
        // disable check in 3-rd party libs
    }
}

easylauncher {
    productFlavors {
        dbcoTest { filters chromeLike(label: "TEST") }
        dbcoAcc { filters chromeLike(label: "ACC") }
        dbcoStaging { filters chromeLike(label: "STAGING") }
        dbcoProd { filters chromeLike(label: "PROD") }
    }
    variants {
        dbcoProdRelease { enable false }
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/lisawray/maven/'
        content {
            includeGroup "com.xwray"
        }
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_libs_version"

    //androidx
    implementation "androidx.security:security-crypto:$androidx_crypto_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.activity:activity-ktx:$androidx_activity_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidx_swiperefresh_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_nav_version"

    //google
    implementation "com.google.android.material:material:$google_material_version"
    dbcoAccImplementation "com.google.firebase:firebase-crashlytics-ktx:$firebase_crashlytics_ktx_version"
    dbcoTestImplementation "com.google.firebase:firebase-crashlytics-ktx:$firebase_crashlytics_ktx_version"

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_json_version")

    //3rd-party dependency versions
    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-viewbinding:$groupie_version"
    implementation "com.github.qiujayen:sticky-layoutmanager:$sticky_header_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$kotlin_serialization_converter_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    implementation "com.scottyab:rootbeer-lib:$rootbeer_version"
    implementation "net.danlew:android.joda:$joda_time_version"
    implementation "com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:$libsodium_jni_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    //test
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    testImplementation "androidx.arch.core:core-testing:$androidx_lifecycle_testing_version"
    testImplementation "io.mockk:mockk:$mockk_version"

    androidTestImplementation "androidx.test.ext:junit:$androidx_test_ext_junit_version"
    androidTestImplementation("androidx.test:runner:1.3.0")
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_espresso_version"
}